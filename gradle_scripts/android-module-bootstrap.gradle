/**
 * This module provides a base for for submodules which depend on the Android runtime
 */
apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'jacoco'
apply from: "${project.rootDir}/gradle_scripts/code_quality.gradle"

android {
    compileSdkVersion versions.compileSdk

    defaultConfig {
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
    }

    compileOptions {
        // Sets Java compatibility to Java 8
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main.java.srcDirs += "${projectDir}/src/main/kotlin"
        test.java.srcDirs += "${projectDir}/src/test/kotlin"
        test.java.srcDirs += "${projectDir}/src/integrationTest/kotlin"
        test.resources.srcDirs += "${projectDir}/src/integrationTest/resources"
    }

    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
        }
        release {
            minifyEnabled false
        }
    }

    buildFeatures {
        buildConfig = false
        viewBinding true
        dataBinding true
    }
}


tasks.withType(Test) {
    useJUnitPlatform()
    jacoco {
        includeNoLocationClasses = true
        excludes += jacocoExclude
    }
}

dependencies {
    api libs.kotlinStdlib

    testImplementation libs.junit
    testRuntimeOnly libs.junitVintage
}

jacoco {
    toolVersion(libs.versions.jacoco.get())
}

ext {
    jacocoExclude = ['jdk.internal.*']
}

