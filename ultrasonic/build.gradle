apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'jacoco-android'
apply plugin: 'org.moallemi.advanced-build-version'
apply from: "../gradle_scripts/code_quality.gradle"

advancedVersioning {
    nameOptions {
        versionMajor 2
        versionMinor 2
        versionPatch 0
    }
    codeOptions {
        versionCodeType org.moallemi.gradle.internal.VersionCodeType.AUTO_INCREMENT_ONE_STEP
    }
    outputOptions {
        renameOutput true
    }
}

android {
    compileSdkVersion versions.compileSdk

    defaultConfig {
        applicationId "org.moire.ultrasonic"
        versionCode advancedVersioning.versionCode
        versionName advancedVersioning.versionName

        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk

        resConfigs "en", "es", "fr", "hu", "pt", "pt-rBR"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
        debug {
            minifyEnabled false
            testCoverageEnabled true
        }
    }

    sourceSets {
        main.java.srcDirs += "${projectDir}/src/main/kotlin"
        test.java.srcDirs += "${projectDir}/src/test/kotlin"
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
    }

    lintOptions {
        baselineFile file("lint-baseline.xml")
        abortOnError true
    }
}

dependencies {
    implementation project(':menudrawer')
    implementation project(':pulltorefresh')
    implementation project(':library')
    implementation project(':subsonic-api')

    implementation androidSupport.support
    implementation androidSupport.design

    implementation other.kotlinStdlib

    testImplementation other.kotlinReflect
    testImplementation testing.junit
    testImplementation testing.kotlinJunit
    testImplementation testing.mockitoKotlin
    testImplementation testing.kluent
}

// Excluding all non-kotlin classes
ext {
    jacocoExclude = [
            '**/activity/**',
            '**/audiofx/**',
            '**/domain/**',
            '**/fragment/**',
            '**/provider/**',
            '**/receiver/**',
            '**/service/**',
            '**/Test/**',
            '**/util/**',
            '**/view/**',
            '**/R$*.class',
            '**/R.class',
            '**/BuildConfig.class'
    ]
}
jacocoAndroidUnitTestReport {
    excludes += jacocoExclude
}

afterEvaluate {
    testDebugUnitTest.finalizedBy jacocoTestDebugUnitTestReport
}
